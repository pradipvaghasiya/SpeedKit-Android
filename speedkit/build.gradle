apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

def version_code = 1
def version_name = '0.1'


def isSnapshot = version.endsWith('-SNAPSHOT')
// For upload to maven central with uploadArchives task, define these values in
// gradle.properties.
def ossrhUsername = hasProperty('ossrhUsername')
def ossrhPassword = hasProperty('ossrhPassword')


ext {
    PUBLISH_GROUP_ID = 'happyfall.speedkit'
    PUBLISH_ARTIFACT_ID = 'speedkit'
    PUBLISH_VERSION = version_name
}

android {

    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 22
        versionCode version_code
        versionName version_name
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

signing {
    required { !isSnapshot && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4' //version required
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment ->
                signing.signPom(deployment)
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                packaging 'aar'
                name 'SpeedKit Android'
                description 'An Android library that speeds up UI development and enforces developer to follow MVVMC (Model-View-ViewModel-Controller) pattern.'
                url 'https://github.com/pradipvaghasiya/SpeedKit-Android/'
                scm {
                    url 'https://github.com/pradipvaghasiya/SpeedKit-Android'
                    connection 'scm:git:https://github.com/bignerdranch/recyclerview-multiselect.git'
                }
                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'pradipvaghasiya'
                        name 'Pradip Vaghasiya'
                        email 'pradip.vaghasiya@gmail.com'
                    }
                }
            }
        }
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.substring(0,1).toUpperCase() + variant.name.substring(1)}Javadoc", type: Javadoc) {
        source = variant.javaCompile.source
        def androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files, androidJar)
        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
        }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}
dependencies {
    compile 'com.android.support:recyclerview-v7:22.1.1'
    //compile 'com.bignerdranch.android:recyclerview-multiselect:+'
}
